  (* Code.5-C: ProVerif-AEAD model *)
set traceDisplay = long.
(* Observational Equivalence: MtE and EtM *)
  free c: channel.
  type key.
  free Kaead: key [private].

(* AEAD: Encryption scheme *)
  fun enc(bitstring, key, bitstring): bitstring.
  reduc forall x:bitstring, k:key, r:bitstring; dec(enc(x,k,r),k,r) = x.

(* AEAD: MAC *)
  fun mac(bitstring, key): bitstring.
  reduc forall x: bitstring, k: key; verify_mac(mac(x, k), x, k) = true.

(* MtE *)
  fun AEAD_MtE_Enc(key,bitstring,bitstring,bitstring):bitstring.
  equation forall k: key, n: bitstring, p:bitstring, ad:bitstring; 
    AEAD_MtE_Enc(k, n, p, ad) = enc((p, mac((n, ad, p), k)), k, n).

  reduc forall k: key, n: bitstring, p:bitstring, ad:bitstring; 
  AEAD_MtE_Dec(enc((p, mac((n, ad, p), k)), k, n), n, ad, k) = p.

  reduc forall k: key, n: bitstring, p:bitstring, ad:bitstring; 
  AEAD_MtE_Verify(enc((p, mac((n, ad, p), k)), k, n), n, ad, k) = true.

  fun AEAD_MtE_Ad(bitstring):bitstring.
  equation forall k:key, n:bitstring, p:bitstring, ad:bitstring; 
    AEAD_MtE_Ad(enc((p, mac((n, ad, p), k)), k, n)) = ad.

  fun AEAD_MtE_N(bitstring):bitstring.
  equation forall k:key, n:bitstring, p:bitstring, ad:bitstring; 
    AEAD_MtE_N(enc((p, mac((n, ad, p), k)), k, n)) = n.

(* EtM *)
  fun AEAD_EtM_Enc(key,bitstring,bitstring,bitstring): bitstring.
  equation forall k: key, n: bitstring, p:bitstring, ad:bitstring; 
  AEAD_EtM_Enc(k, n, p, ad) = 
    (enc(p, k, n), mac((n, ad, enc(p, k, n)), k)).

  reduc forall k: key, n: bitstring, p:bitstring, ad:bitstring; 
  AEAD_EtM_Dec((enc(p, k, n), mac((n, ad, enc(p, k, n)), k)), n, ad, k) 
    = p.

  reduc forall k: key, n: bitstring, p:bitstring, ad:bitstring; 
  AEAD_EtM_Verify((enc(p, k, n), mac((n, ad, enc(p, k, n)), k)), n, ad, k) 
    = true.

  fun AEAD_EtM_Ad(bitstring):bitstring.
  equation forall k:key, n:bitstring, p:bitstring, ad:bitstring; 
    AEAD_EtM_Ad((enc(p, k, n), mac((n, ad, enc(p, k, n)), k))) = ad.

  fun AEAD_EtM_N(bitstring):bitstring.
  equation forall k:key, n:bitstring, p:bitstring, ad:bitstring; 
    AEAD_EtM_N((enc(p, k, n), mac((n, ad, enc(p, k, n)), k))) = n.

(* Encryption Oracle *)
  let processEO_MtE(Kaead:key) =
      in(c, (na:bitstring, ada:bitstring, pa:bitstring)); 
      out(c, AEAD_MtE_Enc(Kaead, na, pa, ada)).

(* Encryption Oracle *)
  let processEO_EtM(Kaead:key) =
      in(c, (na:bitstring, ada:bitstring, pa:bitstring)); 
      out(c, AEAD_EtM_Enc(Kaead, na, pa, ada)).

(* Main Process *)
  process
    (!processEO_MtE(Kaead)) | (!processEO_EtM(Kaead)) | 
    (new t: bitstring; 
     new non:bitstring; 
     new ad:bitstring;
     let CMtE = AEAD_MtE_Enc(Kaead, non, t, ad) in 
     let CEtM = AEAD_EtM_Enc(Kaead, non, t, ad) in 
     out(c, choice[CMtE,CEtM]))
